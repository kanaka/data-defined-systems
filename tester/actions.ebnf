requests   = '[\n' reset-req ',\n' request ( ',\n' request )* '\n]'
reset-req  = '{"method":"PUT",'    '"path":"/reset",'     '"payload":{}}'
request    = '{"method":"POST",'   '"path":"/users",'     '"payload":' post-user '}'  (* {:weight 200} *)
           | '{"method":"PUT",'    '"path":"/users/' id '","payload":' put-user '}'
           | '{"method":"DELETE",' '"path":"/users/' id '"}'
           | '{"method":"GET",'    '"path":"/users/' id '"}'
           | '{"method":"GET",'    '"path":"/users"}'
post-user  = '{"name":"' name '","email":"' email '"}'
put-user   = post-user
           | '{"name":"' name '"}'
           | '{"email":"' email '"}'
id         = nz-number
name       = 'Joe ' #"[A-Z]"
email      = 'joe' #"[0-9]" '@example.com'

nz-number  = nz-digit           (* {:weight 1000} *)
           | '1' any-digit?
any-digit  = "0" | nz-digit
nz-digit   = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
